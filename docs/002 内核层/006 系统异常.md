# 异常的分类 

- 错误（fault）：错误是一种可以被修正的以上。只要错误被修正，处理器可将程序或任务的运行环境还原至异常发生前（已在栈中保存的CS和EIP寄存器值），并重新执行产生异常的指令，也就是异常的返回地址指向错误产生的指令，而不是其后的位置
- 陷阱（trap）：陷阱异常同样允许处理器继续执行任务或程序，只不过处理器对跳过产生异常的指令，即陷阱异常的返回地址指向诱发陷阱指令之后的地址
- 终止（abort）：终止异常用于报告非常严重的错误，它往往无法准确提供产生异常的位置，同时页不允许程序或任务继续执行，典型的终止异常有硬件错误或系统表存在不合逻辑、非法值。

INTEL处理器目前支持的异常/中断：

![Screenshot_20220722_234450](https://img.ansore.top/2022/07/22/bc5d1d360c412730acb325004b854618.png)

![Screenshot_20220722_234501](https://img.ansore.top/2022/07/22/f5e1aef9a88a34939d3f9f2b576486f8.png)

# 系统异常处理

处理器采用类似汇编指令CALL的调用方法来执行异常/中断处理程序。当处理器捕获到异常/中断时，便会根据异常/中断向量号（Interrupt Vector）从中断描述符表IDT索引出对应的门描述符，再由门描述符定位到处理程序的位置。如果向量号索引到一个中断门或陷阱门，处理器将会像执行CALL指令访问调用门一般，去执行异常/中断处理程序。如果向量号索引到一个任务门，处理器将会发生任务切换，转而执行异常任务或中断任务，这个过程就像执行CALL指令访问调用任务门一样。

异常/中断的处理步骤：

![Screenshot_20220723_004245](https://img.ansore.top/2022/07/23/8bc0386abe6cf83df1305f89ea8fbcb8.png)

处理器会根据异常/中断向量号从中断描述符表IDT检索出对应门描述符（中断门或陷阱门），并读取门描述符保存的段选择子。随后从GDT或LDT描述符中检索出处理程序所在的代码段，再根据门描述符记录的段内偏移量，来确定异常/中断异常处理程序的入口地址。

处理器在执行异常/中断处理程序时，会检测异常/中断处理程序所在的代码段的特权级，并与代码段寄存器的特权级进行比较

- 如果异常/中断处理程序的特权级更高，则会在异常/中断处理程序执行前切换栈空间，
  - 处理器会从任务状态段TSS中取出对应特权级的栈段选择子和栈指针，并将它们作为异常/中断处理程序的栈空间进行切换。在栈空间切换过程中，处理器将自动把切换前的SS和ESP寄存器压入异常/中断串处理程序栈
  - 在栈空间切换过程中，处理器还会保存被中断程序的EFLAGS、CS和EIP寄存器值到异常/中断处理程序栈
  - 如果异常会产生错误码，则将其保存在异常任务栈内，位于EIP寄存器之后

- 如果异常/中断处理程序的特权级与代码段寄存器的特权级相等
  - 处理器将保存被中断程序的EFLGAS、CS和EIP寄存器值到栈中
  - 如果异常会产生错误码，则将其保存在异常栈内，位于EIP寄存器之后

![Screenshot_20220723_005856](https://img.ansore.top/2022/07/23/ecd8491af2b0d7b9bc0ecf842828f5f2.png)

处理器必须借助`IRET`指令才能从异常/中断处理程序返回。`IRET`指令会还原之前保存的`EFLAGS`寄存器的值。`EFLAGS`寄存器的`IOPL`标志位只有在`CPL=0`时才可被还原，而IF标志位只有在`CPL<=IOPL`时候才能改变。如果在执行处理程序时发生过栈空间切换，那么执行`IRET`指令将切换回被中断程序栈。

**异常/中断处理的标志位**。当处理器穿过中断门执行异常/中断处理程序时，处理器会在标志寄存器EFLAGS入栈后复位TF标志位，以关闭单步调试功能（处理器还会复位VM、RF和NT标志位）。在执行IRET指令过程中，处理器还会被中断程序的标志寄存器EFLAGS，进而相继还原TF、VM、RF、NT等标志位。

- 当处理器穿过中断门执行异常/中断处理程序时，处理器将复位IF标志位，以防止其他中断请求干扰异常/中断处理程序。处理器在随后执行的IRET指令时，将栈中保存的EFLAGS寄存器值还原，进而置位IF标志位。
- 当处理器穿过陷阱门执行异常/中断处理程序时，处理器却不对复位IF标志位。

中断和异常向量在同一张IDT内，IDT表的前32个向量号被异常占用，而且每个异常的向量号固定不能更改，从向量号32开始被中断处理程序所用。

当异常/中断发生时，执行`ignore_int`模块，显示`unknow interrupt or fault at RIP`提示信息。

## 设置IDT

```assembly
setup_IDT:
  # 使用lea取出ignore_int标识符的基地址
  # 这个基地址要被拆分成第0-15位和第48-63位，分别放在EAX和EDX寄存器中
  # 然后在将EAX的值加載到中断描述符的低位，將EDX的值加載到中断描述符的高位。
  leaq ignore_int(%rip), %rdx
  # 位数: 
  # 0-1 RPL 请求特权级
  # 1-2   TI 指示目标段描述符所在描述符表类型
  # 3-15 用于索引目标段描述符
  # 2
  # 段选择符，我们要选用代码段的段选择符，所以我们使用0008h号GDT段选择符。
  movq $(0x08 << 16), %rax
  # 段内偏移 15:00  %ax =  ignore_int 函数的低16位 %ax = 0x5555 %rax = 0x0000 0000 0008 5555
  movw %dx, %ax
  # %rcx = 0x0000 8E00 0000 0000 = 0b0000 0000 0000 0000 1000 1110 0000 0000
  # 32-34 IST ( Interrupt Stack Table，中断枝表)是IA-32e模式为任务状态段引人的新型战指针，其功能与 RSP相同，只不过IST切换中断棋指针时不会考虑特权级切换。
  # 35-39:0
  # 40-43:Type 第40-43位为段描述符类型标志（TYPE），我们设置的是1110.即将此段描述符标记为“386中断门”。
  # 44-44:0 45-46:DPL 描述符特权级
  # 47-48:P 指定调用门描述符是否有效
  movq $(0x8e00 << 32), %rcx
  # %rax = 0x0000 8E00 0008 5555  %rcx = 0x0000 8E00 0000 0000   
  addq %rcx, %rax
  # %ecx = ignore_int 函数的低32位 0x4444 5555    %rcx = 0x0000 8E00 4444 5555
  movl %edx, %ecx
  # %ecx =  0x0000 4444 %rcx = 0x0000 8E00 0000 4444 
  shrl $16, %ecx
  # %rcx =  4444 0000 0000 0000
  shlq $48, %rcx
  # %rax =  0x4444 8E00 0008 5555
  addq %rcx, %rax
  # %rdx = 0x0000 0000 2222 3333
  shrq $32, %rdx
  leaq IDT_Table(%rip), %rdi
  mov $256, %rcx

rp_sidt:
  movq %rax, (%rdi)
  movq %rdx, 8(%rdi)
  addq $0x10, %rdi
  dec %rcx
  jne rp_sidt
```

`setup_IDT`负责初始化中断描述符表`IDT`内的每个中断描述符（共256项）。它将`ignore_int`模块的起始地址和其他配置信息，有序地格式化成IA-32e模式的中断门描述符结构信息，并把结构信息保存到RAX寄存器（结构信息的低8字节）和RDX寄存器（结构信息的高8字节）中。最后借助`rp_sidt`模块将这256个中断描述符项统一初始化。

中断描述符格式如下：

![Screenshot_20220724_012727](https://img.ansore.top/2022/07/24/1cd64cb18e77498950f7b1d0e8299898.png)

IDT初始化完毕之后，我们还需要对任务状态段描述符`TSS Descriptor`进行初始化。

## 设置TSS

TSS描述如下：

![Screenshot_20220724_014307](https://img.ansore.top/2022/07/24/02e8b7ddfad80e6d6cc303d435de7a55.png)

```assembly
setup_TSS64:
  leaq TSS64_Table(%rip), %rdx
  xorq %rax, %rax
  xorq %rcx, %rcx
  movq $0x89, %rax
  shlq $40, %rax
  movl %edx, %ecx
  shrl $24, %ecx
  shlq $56, %rcx
  addq %rcx, %rax
  xorq %rcx, %rcx
  movl %edx, %ecx
  andl $0xffffff, %ecx
  shlq $16, %rcx
  addq %rcx, %rax
  addq $103, %rax
  leaq GDT_Table(%rip), %rdi
  movq %rax, 64(%rdi)
  shrq $32, %rdx
  movq %rdx, 72(%rdi)

  mov $0x40, %ax
  ltr %ax

  movq go_to_kernel(%rip), %rax
  pushq $0x08
  pushq %rax
  lretq

go_to_kernel:
  .quad start_kernel
```

这部分程序负责初始化`GDT`（IA-32e模式）内的`TSS Descriptor`，并通过`LTR`汇编指令吧`TSS Descriptor`的选择子加载到TR寄存器中。因为当前内核程序已经运行于0特权级，即使产生异常页不会切换任务栈，从而无需访问`TSS`，那么暂且无需初始化`TSS`。在特权级无变化的情况下，即使步加载`TSS Descriptor`的选择子到`TR`寄存器，异常仍可以被捕获并处理。

## 中断处理

中断处理模块：

```assembly
# ignore int
ignore_int:
  # CLD指令即告诉程序si，di向內存地址增大的方向走。
  cld
  pushq %rax
  pushq %rbx
  pushq %rcx
  pushq %rdx
  pushq %rbp
  pushq %rdi
  pushq %rsi

  pushq %r8
  pushq %r9
  pushq %r10
  pushq %r11
  pushq %r12
  pushq %r13
  pushq %r14
  pushq %r15

  movq %es, %rax
  pushq %rax
  movq %ds, %rax
  pushq %rax

  movq $0x10, %rax
  movq %rax, %ds
  movq %rax, %es

  leaq int_msg(%rip), %rax
  pushq %rax
  movq %rax, %rdx
  movq $0x00000000, %rsi
  movq $0x00ff0000, %rdi
  movq $0, %rax
  callq color_printk
  addq $0x8, %rsp

loop:
  jmp loop
  
  popq %rax
  movq %rax, %ds
  popq %rax
  movq %rax, %es

  popq %r15
  popq %r14
  popq %r13
  popq %r12
  popq %r11
  popq %r10
  popq %r9
  popq %r8

  popq %rsi
  popq %rdi
  popq %rbp
  popq %rdx
  popq %rcx
  popq %rbx
  popq %rax
  iretq

int_msg:
  .asciz "unknown interrupt or fault at RIP\n"
```

这段程序先保存各个寄存器的值，然后将`DS`和`ES`段寄存器设置成内核数据段，紧接着将`color_printk`函数准备参数，并采用寄存器传递方式传递参数。显示提示信息后，再执行JMP指令死循环在`ignore_int`模块中。

触发`#DE`（除0）异常：

```c
void start_kernel(void) {
  ...
  i = 1 / 0;
  while (1) {
    ;
  }
}
```

运行结果：

![Screenshot_20220724_010331](https://img.ansore.top/2022/07/24/5e417f4ad0630a4f9b98c64d187a7d9d.png)
